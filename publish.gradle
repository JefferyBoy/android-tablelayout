apply plugin: 'maven-publish'

def localProperties = new Properties()
localProperties.load(rootProject.file("local.properties").newDataInputStream())

// debug仓库
def SNAPSHOT_REPOSITORY_URL = localProperties.get("mavenRepoRelease")
// release仓库
def RELEASE_REPOSITORY_URL = localProperties.get("mavenRepoSnapshots")
// maven 账户
def REPOSITORY_USER_NAME = localProperties.get("mavenUserName")
def REPOSITORY_USER_PSW = localProperties.get("mavenPassword")
def POM_GROUP_ID = 'com.gainscha'
def POM_ARTIFACT_ID = project.name.startsWith("sdk-") ? project.name.substring(4) : project.name
def VERSION_NAME = project.version.toString()
def POM_NAME = POM_ARTIFACT_ID
def POM_DESCRIPTION = ''
def POM_SCM_URL = ''
def POM_DEVELOPER_ID = localProperties.get("mavenUserName")
def POM_DEVELOPER_NAME = localProperties.get("mavenUserName")
def POM_DEVELOPER_EMAIL = localProperties.get("mavenUserEmail")

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release

                groupId = POM_GROUP_ID
                artifactId = POM_ARTIFACT_ID
                version = VERSION_NAME

                pom {
                    name = POM_NAME
                    description = POM_DESCRIPTION
                    url = POM_SCM_URL

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = POM_DEVELOPER_ID
                            name = POM_DEVELOPER_NAME
                            email = POM_DEVELOPER_EMAIL
                        }
                    }
                    scm {
                        url = POM_SCM_URL
                        connection = ""
                        developerConnection = ""
                    }
                }
            }

        }

        repositories {
            maven {
                url = VERSION_NAME.endsWith('SNAPSHOT') ? SNAPSHOT_REPOSITORY_URL : RELEASE_REPOSITORY_URL
                allowInsecureProtocol true
                credentials {
                    username = REPOSITORY_USER_NAME
                    password = REPOSITORY_USER_PSW
                }
            }
        }
    }
}


